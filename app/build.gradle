apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.example.flickrpublicgallery"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            minifyEnabled false
            versionNameSuffix "-debug"
            applicationIdSuffix ".debug"
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
        }
        release {
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'

        }
    }

    flavorDimensions "default"

    productFlavors {
        //mockDebug will used for espresso testing in mock manner, i.e. API calls will be stubbed at client level only
        mock {
            applicationIdSuffix = ".mock"
            dimension "default"
            versionNameSuffix "-mock"
        }

        //prodDebug will used for regular development, manual testing and unit testing
        //prodRelease will be actual play store release
        prod {
            dimension "default"
        }
    }

    // Remove all release variants apart from prodRelease as it's not needed.
    android.variantFilter { variant ->
        if (variant.buildType.name == 'release' && (variant.flavors[0].name != 'prod')) {
            variant.ignore = true
        }
    }

    dataBinding {
        enabled = true
    }

    androidExtensions {
        experimental = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'

    //kotlin libs
    implementation 'androidx.core:core-ktx:1.1.0'
    //Kotlin coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.2.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.2.1"

    //design libraries
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.constraintlayout:constraintlayout-solver:1.1.3'

    //jetpack component
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0-alpha04'
    kapt 'androidx.lifecycle:lifecycle-compiler:2.2.0-alpha04'
    implementation 'androidx.legacy:legacy-support-core-utils:1.0.0'

    //image downloader
    implementation 'com.github.bumptech.glide:glide:4.8.0'

    //logging
    implementation 'com.jakewharton.timber:timber:4.7.1'

    //networking (retrofit)
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'

    //unit testing libs
    testImplementation 'junit:junit:4.12'
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    testImplementation 'org.mockito:mockito-core:2.28.2'
    testImplementation 'com.squareup.retrofit2:retrofit-mock:2.3.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.2.1'

    //UI testing libs
    // Core library
    androidTestImplementation 'androidx.test:core:1.2.0'

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'

    // Assertions
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.ext:truth:1.2.0'
    // Espresso dependencies
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.2.0'
}
